def all_keys = ["pr_id", "pr_id_url", "REPO", "testcase_url", "DST_PR", "DST_PR_SHA"]

node('jenkins-agent-gh-docker') {
    stage("checkout scm") {
        cleanWs()
        checkout scm
    }
    
    stage("properties") {
        if (params.WEBHOOK_TOKEN == '') {
            error 'WEBHOOK_TOKEN is empty'
        }
        
        def webhook_token = params.WEBHOOK_TOKEN ?: 'N7mDZmUqGPU$mXD'

        echo "Webhook token: ${webhook_token}"

        properties([
            parameters([
                string(defaultValue: webhook_token, description: 'webhook 请求token', name: 'WEBHOOK_TOKEN', trim: true),
            ]),
            pipelineTriggers([
                GenericTrigger(
                    causeString: 'Generic Cause',
                    genericHeaderVariables: [[key: 'X-GitHub-Event', regexpFilter: '']],
                    genericVariables: [[defaultValue: '', key: 'payload', regexpFilter: '', value: '$']],
                    regexpFilterExpression: '',
                    regexpFilterText: '',
                    silentResponse: true,
                    printPostContent: true,
                    token: webhook_token,
                    tokenCredentialId: '',
                )
            ])
        ])
    }

    stage("parse webhook") {
        withCredentials([string(credentialsId: 'rvck_gh_token', variable: 'GH_TOKEN')]) {
            sh 'python3 RVCI/RVCK/rvck-webhook/run.py'
        }
    }
    
    def kernel_build_params = all_keys.findAll{ fileExists(it) }.collect {
        string(name: it, value: readFile(it))
    }
    if(kernel_build_params.size()) {
        stage("run kernel and gh_actions") {
            build wait: false, job: 'gh_actions', parameters: [
                string(name: 'REPO', value: readFile('REPO')),
                string(name: 'ISSUE_ID', value: readFile('pr_id')),
                string(name: 'COMMENT_CONTENT', value: '开始测试'),
                string(name: 'SET_LABEL', value: 'kernel_building,lava_waiting'),
            ]

            build wait: false, job: 'rvck-kernel-build', parameters: kernel_build_params
        }
    }
}
